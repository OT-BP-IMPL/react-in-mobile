plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'

    /**
     * This plugin allows you to run npm commands from gradle.
     * It's used by the npmInstall and buildWeb gradle tasks.
     */
    id "com.github.node-gradle.node"
}

/*
 * Node plugin configuration
 * https://github.com/node-gradle/gradle-node-plugin/blob/master/docs/usage.md#configuring-the-plugin
 */
node {
    // The npm command executed by the npmInstall task
    npmInstallCommand = "install --prefix ../../web"
}

/**
 * This task builds the vite project at ../../web folder.
 */
task buildWeb(type: NpmTask) {
    println 'Running npm build in ../../web'
    args = ['run', 'build', '--prefix', '../../web']
}

/**
 * This task clears the Android assets folder.
 * This is necessary because the Android build process does not clear the assets folder.
 */
task clearAssets(type: Delete) {
    delete 'src/main/assets/dist'
}

/**
 * This task copies the the vite project at ../../web/dist folder to the Android assets folder.
 */
task copyDistToAssets(type: Copy) {
    from '../../web/dist'
    into 'src/main/assets/dist'
}

/**
 * Generates web assets
 */
task prebuildWeb {
    dependsOn npmInstall
    dependsOn buildWeb
    dependsOn clearAssets
    dependsOn copyDistToAssets
    tasks.findByName('clearAssets').mustRunAfter 'buildWeb'
    tasks.findByName('copyDistToAssets').mustRunAfter 'clearAssets'
}

/*
 * Hooks the custom `prebuildWeb` task into running before the app build.
 */
preBuild.dependsOn prebuildWeb

android {
    namespace 'live.ditto.host'
    compileSdk 34

    defaultConfig {
        applicationId "live.ditto.host"
        minSdk 30
        targetSdk 34
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        // https://developer.android.com/jetpack/androidx/releases/compose-kotlin
        kotlinCompilerExtensionVersion '1.4.4'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {

    // You will need this to load local JS and CSS files in the WebView
    implementation "androidx.webkit:webkit:1.8.0"

    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.2'
    implementation 'androidx.activity:activity-compose:1.7.2'
    implementation platform('androidx.compose:compose-bom:2023.09.01')
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.material3:material3'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation platform('androidx.compose:compose-bom:2023.09.01')
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'
}
